#!/usr/bin/env wa2
!version 0.1

!namespace example.com/min

// -----------------------------------------------------------------------------
// Source context (applies to this document)
// -----------------------------------------------------------------------------
source {
	template = "infra.yml"
}

// -----------------------------------------------------------------------------
// Minimal system vocabulary
// -----------------------------------------------------------------------------
namespace data {

	// how sensitive the data is (confidentiality)
	type sensitivity: enum {
		Public,
		Proprietary,
		Private,
		Confidential,
		Sensitive
	}

	// how bad it is to lose the data (availability / impact)
	type criticality: enum {
		MissionCritical,   // existential / legal / safety impact
		BusinessCritical,  // major financial or operational impact
		Important,         // painful but survivable
		NonCritical,       // inconvenience only
		Disposable         // safe to lose
	}
}

namespace vendor {
	namespace aws {
		namespace s3 {

			// S3 bucket is storage
			store bucket {
				field sensitivity: data/sensitivity
				field criticality: data/criticality
				field backed_up: flag
			}
		}
	}
}

// -----------------------------------------------------------------------------
// Policy
// -----------------------------------------------------------------------------
policy critical_storage_is_backed_up {

	// critical storage
	let critical = ?(
		//store[
			@criticality = MissionCritical
			or @criticality = BusinessCritical
		]
	)

	// assert critical storage is backed up
	assert ?(
		not(
			exists(
				critical[ not @backed_up ]
			)
		)
	)
}

// -----------------------------------------------------------------------------
// User resources (mapped from CloudFormation)
// -----------------------------------------------------------------------------

new audit_logs: vendor/aws/s3/bucket {
	@#source/cfn("AuditLogsBucket")
	sensitivity = Sensitive
	criticality = MissionCritical
	backed_up   = true
}

new pii_exports: vendor/aws/s3/bucket {
	@#source/cfn("PiiExportsBucket")
	sensitivity = Sensitive
	criticality = BusinessCritical
	// backed_up missing â†’ policy violation
}
